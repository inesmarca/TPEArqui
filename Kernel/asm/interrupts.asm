
GLOBAL _cli
GLOBAL _sti
GLOBAL picMasterMask
GLOBAL picSlaveMask
GLOBAL haltcpu
GLOBAL _hlt

GLOBAL _irq00Handler
GLOBAL _irq01Handler
GLOBAL _irq02Handler
GLOBAL _irq03Handler
GLOBAL _irq04Handler
GLOBAL _irq05Handler
GLOBAL _irq80Handler

GLOBAL _exception0Handler

EXTERN irqDispatcher
EXTERN exceptionDispatcher
EXTERN readKey
EXTERN writeString
EXTERN getPixelData
EXTERN printPixel
EXTERN getExitFlag
EXTERN changeScreen
EXTERN getTemperature
EXTERN getRegVec


SECTION .text

%macro pushState 0
	push rax
	push rbx
	push rcx
	push rdx
	push rbp
	push rdi
	push rsi
	push r8
	push r9
	push r10
	push r11
	push r12
	push r13
	push r14
	push r15
%endmacro

%macro popState 0
	pop r15
	pop r14
	pop r13
	pop r12
	pop r11
	pop r10
	pop r9
	pop r8
	pop rsi
	pop rdi
	pop rbp
	pop rdx
	pop rcx
	pop rbx
	pop rax
%endmacro

%macro pushReg 0
	push rax
	push rcx
	push rdx
	push rbp
	push rdi
	push rsi
	push r8
	push r9
	push r10
	push r11
	push r12
	push r13
	push r14
	push r15
%endmacro

%macro popReg 0
	pop r15
	pop r14
	pop r13
	pop r12
	pop r11
	pop r10
	pop r9
	pop r8
	pop rsi
	pop rdi
	pop rbp
	pop rdx
	pop rcx
	pop rax
%endmacro

%macro irqHandlerMaster 1
	pushState

	mov rdi, %1 ; pasaje de parametro
	mov rsi, rsp ; pasaje del stack frame
	call irqDispatcher

	; signal pic EOI (End of Interrupt)
	mov al, 20h
	out 20h, al
	
	popState
	iretq
%endmacro

%macro sysCallHandler 0
	pushReg

	cmp rax, 0
	je .runRead		; read del keyboard
	cmp rax, 1
	je .runWrite   	; write de letra
	cmp rax, 2
	je .getPixel	; read de pixel de pantalla
	cmp rax, 3
	je .pixelWrite		; write de pixel
	cmp rax, 4
	je .getExit
	cmp rax, 5
	je .switchScreen	; cambia el string
	cmp rax, 6
	je .cputemp	; devuelve la temp del cpu
	cmp rax, 7
	je .registers
	jmp .fin

.runRead:
	call readKey
	jmp .fin

.runWrite:
	call writeString
	jmp .fin

.getPixel:
	call getPixelData
	jmp .fin

.pixelWrite:
	call printPixel
	jmp .fin

.getExit:
	call getExitFlag
	mov rbx, rax
	jmp .fin

.switchScreen:
	call changeScreen
	jmp .fin
.cputemp:
	call getTemperature
	mov rbx, rax
	jmp .fin

.registers:
	call getRegVec
	jmp .fin

.fin:
	; signal pic EOI (End of Interrupt)
	mov al, 20h
	out 20h, al

	popReg
	iretq

%endmacro

%macro exceptionHandler 1
	pushState

	mov rdi, %1 ; pasaje de parametro
	mov rsi, rsp ; puntero del stack para poder retornar 
	call exceptionDispatcher

	popState
	iretq
%endmacro


_hlt:
	sti
	hlt
	ret

_cli:
	cli
	ret


_sti:
	sti
	ret

picMasterMask:
	push rbp
    mov rbp, rsp
    mov ax, di
    out	21h,al
    pop rbp
    retn

picSlaveMask:
	push    rbp
    mov     rbp, rsp
    mov     ax, di  ; ax = mascara de 16 bits
    out	0A1h,al
    pop     rbp
    retn


;8254 Timer (Timer Tick)
_irq00Handler:
	irqHandlerMaster 0

;Keyboard
_irq01Handler:
	irqHandlerMaster 1

;Cascade pic never called
_irq02Handler:
	irqHandlerMaster 2

;Serial Port 2 and 4
_irq03Handler:
	irqHandlerMaster 3

;Serial Port 1 and 3
_irq04Handler:
	irqHandlerMaster 4

;USB
_irq05Handler:
	irqHandlerMaster 5

;INT 80h
_irq80Handler:
	sysCallHandler

;Zero Division Exception
_exception0Handler:
	exceptionHandler 0

haltcpu:
	cli
	hlt
	ret



SECTION .bss
	aux resq 1